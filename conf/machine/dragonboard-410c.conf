#@TYPE: Machine
#@NAME: dragonboard-410c
#@DESCRIPTION: Machine configuration for the DragonBoard 410c (96boards), with Qualcomm Snapdragon 410 APQ8016.

require conf/machine/include/arm/arch-armv8.inc


XSERVER_OPENGL ?= "xf86-video-freedreno \
                   xserver-xorg-module-exa \
                   xserver-xorg-extension-glx \
                   xserver-xorg-extension-dri \
                  "

XSERVER ?= "xserver-xorg \
            xserver-xorg-module-libint10 \
            ${@bb.utils.contains('DISTRO_FEATURES', 'opengl', '${XSERVER_OPENGL}', 'xf86-video-fbdev', d)} \
            xf86-input-evdev \
            xf86-input-mouse \
            xf86-input-keyboard"

MACHINE_FEATURES = "usbhost usbgadget alsa screen wifi bluetooth ext2"

PREFERRED_PROVIDER_virtual/egl ?= "mesa"
PREFERRED_PROVIDER_virtual/libgl ?= "mesa"
PREFERRED_PROVIDER_virtual/libgles1 ?= "mesa"
PREFERRED_PROVIDER_virtual/libgles2 ?= "mesa"
PREFERRED_PROVIDER_virtual/xserver ?= "xserver-xorg"
PREFERRED_PROVIDER_virtual/kernel ?= "linux-linaro-qcomlt"

KERNEL_IMAGETYPE = "Image"
KERNEL_DEVICETREE = "qcom/apq8016-sbc.dtb"

SERIAL_CONSOLE = "115200 ttyMSM0"

MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS += " \
                                        kernel-modules \
                                        ${@bb.utils.contains('DISTRO_FEATURES', 'opengl', 'mesa-driver-msm', '', d)} \
                                        ${@bb.utils.contains('DISTRO_FEATURES', 'wifi', 'wcnss-config wcnss-start', '', d)} \
                                        ${@bb.utils.contains('DISTRO_FEATURES', 'bluez5', 'bluez5-noinst-tools', '', d)} \
                                        ${@'firmware-qcom-dragonboard410c' if d.getVar('ACCEPT_EULA_dragonboard-410c', True) == '1' else ''}"

CMDLINE = "console=ttyMSM0,115200n8 root=/dev/mmcblk0p10 rootwait"

# Fastboot expects an ext4 image, which needs to be 4096 aligned
IMAGE_FSTYPES_append = " ext4.gz"
IMAGE_ROOTFS_ALIGNMENT = "4096"

